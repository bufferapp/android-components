apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'maven-publish'

apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

def getKey(String key) {
    def Properties props = new Properties()
    props.load(new FileInputStream(file('../local.properties')))
    return props[key]
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/app-release.aar")
            groupId 'org.buffer.android'
            artifactId 'android-components'
            version '1.12'

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = getKey("BINTRAY_USER")
    key = getKey("BINTRAY_KEY")
    publications = ['Production']
    pkg {
        repo = 'Maven'
        name = 'android-components'
        userOrg = 'buffer'
        vcsUrl = 'https://github.com/bufferapp/android-components'
        publish = true
        publicDownloadNumbers = true
        version {
            name = '1.12'
            released  = new Date()
            vcsTag = '1.12'
        }
    }
}
